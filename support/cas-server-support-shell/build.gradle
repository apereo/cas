
apply plugin: "org.springframework.boot"

def MAIN_CLASS = "org.apereo.cas.CasCommandLineShellApplication"

description = "Apereo CAS Shell Support via Spring Shell"

dependencies {
    /**
     * Marking all dependencies as compile so they can be included in the final
     * executable jar by the build packaging process.
     */
    compile project(":api:cas-server-core-api-services")

    compile project(":core:cas-server-core-configuration")
    compile project(":core:cas-server-core-configuration-api")
    compile project(":core:cas-server-core-util")
    compile project(":core:cas-server-core-services-api")
    compile project(":core:cas-server-core-configuration-metadata-repository")

    compile project(":support:cas-server-support-oauth-services")
    compile project(":support:cas-server-support-oidc-services")
    compile project(":support:cas-server-support-saml-idp-core")
    compile project(":support:cas-server-support-ws-idp-api")
    compile project(":support:cas-server-support-saml-idp-core")
    
    compile libraries.springbootconfigmetadata
    compile libraries.metrics
    compile libraries.pac4j
    compile libraries.jasypt
    compile libraries.bouncycastle
    compile libraries.opensaml
    implementation libraries.snakeyaml
}

springBoot {
    mainClass = MAIN_CLASS
}

bootRepackage {
    mainClass = MAIN_CLASS
    enabled = !rootProject.publishingRelease
}

bootRun {
    systemProperties = System.properties
    def list = []
    if (rootProject.enableRemoteDebugging) {
        list.add("-Xrunjdwp:transport=dt_socket,address=5007,server=y,suspend=n")
    }
    jvmArgs = list
}

task listUndocumentedProperties(type: JavaExec) {
    standardOutput = new ByteArrayOutputStream()
    main = "org.apereo.cas.CasCommandLineShellApplication"
    classpath = sourceSets.main.runtimeClasspath
    args "-skb", "-pundoc"
    doLast {
        def theOutput = standardOutput.toString()
        if (theOutput != null && theOutput.length() > 0 && theOutput.contains("Undocumented Property:")) {
            throw new GradleException("Undocumented properties found:\n" + theOutput)
        }
    }
}

//jar.finalizedBy(listUndocumentedProperties)