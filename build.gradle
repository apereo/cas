description = "Apereo Central Authentication Service"

defaultTasks "clean", "build"

ext {
    publishSnapshots = Boolean.getBoolean("publishSnapshots")
    publishReleases = Boolean.getBoolean("publishReleases")
    sonatypeUsername = System.getProperty("sonatypeUsername")
    sonatypePassword = System.getProperty("sonatypePassword")
}

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://maven.eveoh.nl/content/repositories/releases"
        }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.akhikhl.gretty:gretty:1.2.4"
        classpath "nl.eveoh:gradle-aspectj:1.6"
    }
}

allprojects {
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "maven"
    apply plugin: "signing"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'
    apply plugin: 'aspectj'
    apply plugin: 'project-report'
    apply from: rootProject.file("gradle/tasks.gradle")

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url "http://repo.maven.apache.org/maven2" }
        maven { url "https://jitpack.io" }
        maven { url "http://developer.jasig.org/repo/content/groups/m2-legacy/" }
        maven { url "https://build.shibboleth.net/nexus/content/groups/public" }
        maven { url "http://files.couchbase.com/maven2" }
    }

    test {
        enabled = !Boolean.getBoolean("skipTests")
        useJUnit()
        scanForTestClasses false
        include '**/*Tests.*'
        exclude '**/*Abstract*.*'
        classpath = it.classpath + files("${projectDir}/src/main/resources") +
                files("${projectDir}/src/test/resources")

    }

    configurations {
        tests

        provided
        compile.extendsFrom(provided)
    }

    configurations.all {
        resolutionStrategy {
            if (!Boolean.getBoolean("skipVersionConflict")) {
                failOnVersionConflict()
            }
        }
        exclude(group: "cglib", module: "cglib")
        exclude(group: "cglib", module: "cglib-full")
    }

    artifacts {
        tests testJar

        archives sourcesJar
        archives javadocJar
        archives testJar
    }

    signing {
        required = (publishReleases && sonatypeUsername != null && sonatypePassword != null)
        sign configurations.archives
    }

    sourceSets {
        test.resources.srcDirs = ["${projectDir}/src/test/resources", "${projectDir}/src/test/java"]
    }

    javadoc {
        classpath += configurations.provided
    }

    jar {
        manifest {
            attributes("Implementation-Title": project.name,
                    "Implementation-Vendor": project.group,
                    "Created-By": project.group,
                    "Implementation-Date": new Date(),
                    "Implementation-Version": project.version)
        }
    }

    checkstyle {
        configFile new File(rootDir, "checkstyle-rules.xml")
        configProperties = ["checkstyle.suppressions.file": new File(rootDir, "checkstyle-suppressions.xml")]
        ignoreFailures false
        showViolations true
        toolVersion "${checkstyleVersion}"
    }

    checkstyleTest {
        enabled = !Boolean.getBoolean("skipCheckstyle")
    }

    checkstyleMain {
        enabled = !Boolean.getBoolean("skipCheckstyle")
    }

    findbugs {
        toolVersion = "${findbugsVersion}"
        sourceSets = [sourceSets.main]
        ignoreFailures = true
        reportsDir = file("$project.buildDir/findbugsReports")
        effort = "max"
        reportLevel = "high"
        visitors = ["FindSqlInjection", "SwitchFallthrough"]
        omitVisitors = ["FindNonShortCircuit"]
        includeFilter = file("$rootProject.projectDir/findbugs-rules.xml")
    }

    findbugsMain {
        enabled = !Boolean.getBoolean("skipFindbugs")
    }

    findbugsTest {
        enabled = !Boolean.getBoolean("skipFindbugs")
    }

    dependencies {
        compile group: 'org.aspectj', name: 'aspectjrt', version: aspectjVersion
        compile group: 'org.aspectj', name: 'aspectjweaver', version: aspectjVersion
        compile group: 'javax.validation', name: 'validation-api', version: javaxValidationVersion
        compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

        if (!Boolean.getBoolean("skipAspectJ")) {
            compile(group: 'org.jasig.inspektr', name: 'inspektr-aspects', version: inspektrVersion) {
                exclude(group: 'javax.validation', module: 'validation-api')
                exclude(group: 'org.aspectj', module: 'aspectjrt')
            }

            aspectpath(group: 'org.jasig.inspektr', name: 'inspektr-aspects', version: inspektrVersion) {
                exclude(group: 'javax.validation', module: 'validation-api')
                exclude(group: 'org.aspectj', module: 'aspectjrt')
            }
        }

        if (!Boolean.getBoolean("skipFindbugs")) {
            findbugs group: 'com.google.code.findbugs', name: 'findbugs', version: findbugsVersion
            findbugs configurations.findbugsPlugins.dependencies
            findbugsPlugins group: 'com.mebigfatguy.fb-contrib', name: 'fb-contrib', version: findbugsContribVersion
            findbugsPlugins group: 'com.h3xstream.findsecbugs', name: 'findsecbugs-plugin', version: findbugsSecVersion
        }

        runtime(group: 'org.glassfish.web', name: 'javax.el', version: javaxElImplVersion) {
            exclude(group: 'javax.el', module: 'javax.el-api')
        }
        runtime group: 'org.slf4j', name: 'jul-to-slf4j', version: slf4jVersion
        runtime group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
        runtime group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
        runtime(group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4jVersion) {
            exclude(group: 'org.slf4j', module: 'slf4j-api')
        }
        runtime group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: log4jVersion
        testCompile group: 'junit', name: 'junit', version: junitVersion
        testCompile(group: 'org.mockito', name: 'mockito-core', version: mockitoVersion) {
            exclude(group: 'org.hamcrest', module: 'hamcrest-core')
        }
        testCompile group: 'org.springframework', name: 'spring-test', version: springVersion
        provided group: 'javax.servlet', name: 'javax.servlet-api', version: javaxSevletVersion
        provided group: 'javax.el', name: 'javax.el-api', version: javaxElVersion
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                if (publishReleases) {
                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: sonatypeUsername, password: sonatypePassword);
                    }
                }

                if (publishSnapshots) {
                    snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                        authentication(userName: sonatypeUsername, password: sonatypePassword);
                    }
                }

                pom.project {
                    url "http://www.apereo.org/cas"
                    inceptionYear 2004
                    name project.name
                    description project.name
                    issueManagement {
                        system "Github"
                        url "https://github.com/Jasig/cas/issues"
                    }

                    scm {
                        url 'scm:git@github.com:Jasig/cas.git'
                        connection 'scm:git@github.com:Jasig/cas.git'
                        developerConnection 'scm:git@github.com:Jasig/cas.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'leleuj'
                            name 'Jérôme Leleu'
                        }
                        developer {
                            id 'apetro'
                            name 'Andrew Petro'
                        }
                        developer {
                            id 'wgthom'
                            name 'William G. Thompson'
                        }
                        developer {
                            id 'serac'
                            name 'Marvin S. Addison'
                        }
                        developer {
                            id 'battags'
                            name 'Scott Battaglia'
                        }
                        developer {
                            id 'mmoayyed'
                            name 'Misagh Moayyed'
                        }
                        developer {
                            id 'robertoschwald'
                            name 'Robert Oschwald'
                        }
                    }

                    contributors {
                        contributor {
                            name "Adam Rybicki"
                            email "arybicki@unicon.net"
                        }
                        contributor {
                            name "Andrew Tillinghast"
                            email "atilling@conncoll.edu"
                        }
                        contributor {
                            name "Arnaud Lesueur"
                            email "arnaud.lesueur@gmail.com"
                        }
                        contributor {
                            name "Daniel Frett"
                            email "daniel.frett@ccci.org"
                        }
                        contributor {
                            name "Dmitriy Kopylenko"
                            email "dima767@gmail.com"
                        }
                        contributor {
                            name "Drew Mazurek"
                            email "dmazurek@unicon.net"
                        }
                        contributor {
                            name "Eric Dalquist"
                            email "eric.dalquist@doit.wisc.edu"
                        }
                        contributor {
                            name "Eric Pierce"
                            email "epierce@usf.edu"
                        }
                        contributor {
                            name "Frederic Esnault"
                            email "esnault.frederic@gmail.com"
                        }
                        contributor {
                            name "Howard Gilbert"
                            email "Howard.Gilbert@yale.edu"
                        }
                        contributor {
                            name "Jan Van der Velpen"
                            email "velpi@industria.be"
                        }
                        contributor {
                            name "John Martin"
                            email "jmartin@unicon.net"
                        }
                        contributor {
                            name "MarcAntoine Garrigue"
                            email "marc.antoine.garrigue@gmail.com"
                        }
                        contributor {
                            name "Mihir Patel"
                            email "exploremihir@gmail.com"
                        }
                        contributor {
                            name "Fredrik Jönsson"
                            email "fjo@kth.se"
                        }
                    }

                    mailingLists {
                        mailingList {
                            name "cas-user"
                            subscribe "https://groups.google.com/a/apereo.org/forum/#!forum/cas-user"
                            unsubscribe "https://groups.google.com/a/apereo.org/forum/#!forum/cas-user"
                            post "cas-user@apereo.org"
                            archive "https://groups.google.com/forum/#!forum/jasig-cas-user"
                        }
                        mailingList {
                            name "cas-dev"
                            subscribe "https://groups.google.com/a/apereo.org/forum/#!forum/cas-dev"
                            unsubscribe "https://groups.google.com/a/apereo.org/forum/#!forum/cas-dev"
                            post "cas-dev@apereo.org"
                            archive "https://groups.google.com/forum/#!forum/jasig-cas-dev"
                        }
                        mailingList {
                            name "cas-announce"
                            subscribe "https://groups.google.com/a/apereo.org/forum/#!forum/cas-announce"
                            unsubscribe "https://groups.google.com/a/apereo.org/forum/#!forum/cas-announce"
                            post "cas-announce@apereo.org"
                            archive "https://groups.google.com/a/apereo.org/forum/#!forum/cas-announce"
                        }
                    }

                    repositories {
                        repository {
                            id "shib-release"
                            url "https://build.shibboleth.net/nexus/content/repositories/releases"
                            releases {
                                enabled true
                            }
                       }
                       repository {
                            id "jitpack"
                            url "https://jitpack.io"
                            releases {
                                enabled true
                            }
                       }  
                       repository {
                            id "jasig-legacy"
                            url "http://developer.jasig.org/repo/content/groups/m2-legacy"
                            releases {
                                enabled true
                            }
                       }  
                       repository {
                            id "couchbase"
                            url "http://files.couchbase.com/maven2/"
                            releases {
                                enabled true
                            }
                       } 
                    }

                }
            }
        }

    }

}

/**
 * Aggregate all javadocs into a single directory
 **/
task javadoc(type: Javadoc) {
    source subprojects.collect { project -> project.sourceSets.main.allJava }
    destinationDir = new File(buildDir, 'javadoc')
    classpath = files(subprojects.collect { project -> project.sourceSets.main.compileClasspath })
} 