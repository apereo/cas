import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: "war"
apply plugin: "org.springframework.boot"

description = "Apereo CAS Web Application"

sourceSets {
    main {
        resources {
            srcDirs "${project.buildDir}/generated-src/main/resources", "${project.projectDir}/../resources"
        }
    }
}

javadoc {
    enabled false
}


if (Boolean.getBoolean("enableJRebelAgent")) {
    war.dependsOn(generateRebel)
}

springBoot  {
    mainClass = "org.apereo.cas.web.CasWebApplication"
}

bootRepackage {
    mainClass = "org.apereo.cas.web.CasWebApplication"
    enabled = !rootProject.publishReleases
}

bootRun {

    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        dependsOn pathingJar
        doFirst {
          classpath = files("$buildDir/classes/main", "$buildDir/resources/main", pathingJar.archivePath)
        }
    }

    addResources = true
    systemProperties = System.properties

    def list = []
    if (Boolean.getBoolean("enableRemoteDebugging")) {
        list.add("-Xrunjdwp:transport=dt_socket,address=5000,server=y,suspend=n")
    }
    if (Boolean.getBoolean("enableJRebelAgent")) {
        def lib = System.getProperty("JREBEL_LIB_PATH")
        list.add("-agentpath:$lib")
        list.add("-Drebel.remoting_plugin=true")
    }
    jvmArgs = list
}

war {
    def currentTime = java.time.ZonedDateTime.now()

    manifest {
        attributes("Implementation-Title": project.name,
                "Implementation-Vendor": project.group,
                "Created-By": project.group,
                "Implementation-Date": currentTime,
                "Specification-Version": rootProject.currentRevision,
                "Implementation-Version": project.version)
    }
    from "${project.buildDir}/generated-src/main/resources", { into "WEB-INF/classes" }
    enabled = !rootProject.publishReleases && !rootProject.publishSnapshots
}

dependencies {
    compile project(":core:cas-server-core")
    compile project(":core:cas-server-core-monitor")
    compile project(":core:cas-server-core-authentication")
    compile project(":core:cas-server-core-services")
    compile project(":core:cas-server-core-logout")
    compile project(":core:cas-server-core-audit")
    compile project(":core:cas-server-core-logging")
    compile project(":core:cas-server-core-tickets")
    compile project(":core:cas-server-core-web")
    compile project(":core:cas-server-core-validation")
    compile project(":core:cas-server-core-util")
    compile project(":core:cas-server-core-events")
    compile project(":core:cas-server-core-events-configuration")
    compile project(":core:cas-server-core-configuration")

    compile project(":support:cas-server-support-throttle")
    compile project(":support:cas-server-support-configuration")
    compile project(":support:cas-server-support-geolocation")
    compile project(":support:cas-server-support-actions")
    compile project(":support:cas-server-support-cookie")
    compile project(":support:cas-server-support-reports")
    compile project(":support:cas-server-support-themes")
    compile project(":support:cas-server-support-validation")
    compile project(":support:cas-server-support-pm")
    
    compile project(":webapp:cas-server-webapp-config")
    compile project(":webapp:cas-server-webapp-init")

    implementation libraries.pac4j
    implementation libraries.thymeleaf
    implementation libraries.groovy
    implementation libraries.cassecurityfilter
    implementation libraries.metrics
    implementation libraries.bouncycastle
    runtime libraries.webjars
    compile libraries.springcloudconfigclient
}
