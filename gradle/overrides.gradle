configurations.all {
    resolutionStrategy {
        if (!providers.systemProperty("skipVersionConflict").present) {
            failOnVersionConflict()
        }
        preferProjectModules()

        def dependencyCacheDays = Integer.valueOf(project.dependencyCacheDays)
        cacheDynamicVersionsFor dependencyCacheDays, 'days'
        cacheChangingModulesFor dependencyCacheDays, 'days'

        def dependencyUpdates = gradle.startParameter.taskNames.any {task -> task.equalsIgnoreCase('dependencyUpdates') }
        if (!dependencyUpdates) {
            eachDependency { DependencyResolveDetails details ->
                def requested = details.requested
                if (requested.group == "org.bouncycastle" && requested.name.endsWith("-jdk15on")) {
                    details.useTarget group: requested.group, name: requested.name.replaceAll("-jdk15on","-jdk18on"), version: "${bouncyCastleVersion}"
                }
                if (requested.group == "gnu-getopt")  {
                    details.useTarget group: "gnu.getopt", name: "java-getopt", version: requested.version
                }
                if (requested.group == "org.opensaml")  {
                    details.useVersion("${opensamlVersion}")
                }
                if (requested.group == "jakarta.activation")  {
                    details.useVersion("${jakartaActivationVersion}")
                }
                if (requested.group == "com.h2database")  {
                    details.useVersion("${h2Version}")
                }
                if (requested.name == "hibernate-validator") {
                    details.useVersion("${hibernateValidatorVersion}")
                }
                if (requested.group == "org.hibernate.orm")  {
                    details.useVersion("${hibernateVersion}")
                }
                if (requested.group == "com.squareup.okhttp3")  {
                    details.useVersion("${okhttp3Version}")
                }
                if (requested.group == "org.postgresql")  {
                    details.useVersion("${postgresqlVersion}")
                }
                if (requested.group == "com.hazelcast" && requested.name == "hazelcast")  {
                    details.useVersion("${hazelcastVersion}")
                }
                if (requested.group == "org.pac4j" && requested.name.startsWith("pac4j-"))  {
                    details.useVersion("${pac4jVersion}")
                }
                if (requested.group == "org.springframework")  {
                    details.useVersion("${springVersion}")
                }
                if (requested.group == "org.springframework.session")  {
                    if (requested.name == "spring-session-data-mongodb") {
                        details.useVersion("${springSessionMongoVersion}")
                    } else {
                        details.useVersion("${springSessionVersion}")
                    }
                }
                if (requested.group.startsWith("org.eclipse.jetty")) {
                    details.useVersion("${jettyVersion}")
                }
                if (requested.group.startsWith("io.undertow")) {
                    details.useVersion("${undertowVersion}")
                }
                if (requested.group.startsWith( "org.apache.tomcat")) {
                    if (requested.name != "jakartaee-migration") {
                        details.useVersion("${tomcatVersion}")
                    }
                }
                if (requested.group == "org.hsqldb")  {
                    details.useVersion("${hsqlVersion}")
                }
                if (requested.group == "org.mariadb.jdbc")  {
                    details.useVersion("${mariaDbVersion}")
                }
                if (requested.group == "com.microsoft.sqlserver")  {
                    details.useVersion("${mssqlServerVersion}")
                }
                if (requested.group == "com.oracle.ojdbc:ojdbc10")  {
                    details.useVersion("${oracleJdbcVersion}")
                }
                if (requested.group == "io.netty")  {
                    if (!requested.name.startsWith("netty-tcnative")) {
                        details.useVersion("${nettyVersion}")
                    }
                }
                if (requested.group == "org.apache.kafka") {
                    details.useVersion("${kafkaVersion}")
                }
                if (requested.group == "org.mongodb")  {
                    details.useVersion("${mongoDriverVersion}")
                }
                if (requested.group == "mysql")  {
                    details.useVersion("${mysqlVersion}")
                }
                if (requested.group == "net.sourceforge.jtds")  {
                    details.useVersion("${jtdsVersion}")
                }
                if (requested.group == "com.couchbase.client" && requested.name == "java-client")  {
                    details.useVersion("${couchbaseVersion}")
                }
                if (requested.group == "com.github.ben-manes.caffeine")  {
                    details.useVersion("${caffeineVersion}")
                }
                if (requested.group == "org.apache.logging.log4j")  {
                    details.useVersion("${log4jVersion}")
                }
                if (requested.group == "org.apache.groovy")  {
                    details.useVersion("${groovyVersion}")
                }
                if (requested.group == "com.zaxxer")  {
                    details.useVersion("${hikariVersion}")
                }
                if (requested.group == "org.thymeleaf")  {
                    details.useVersion("${thymeleafVersion}")
                }
                if (requested.group == "nz.net.ultraq.thymeleaf" && requested.name == "thymeleaf-layout-dialect")  {
                    details.useVersion("${thymeleafDialectVersion}")
                }
                if (requested.group == "commons-beanutils" && requested.name == "commons-beanutils")  {
                    details.useVersion("${commonsBeansVersion}")
                }
                if (requested.group == "ch.qos.logback")  {
                    details.useVersion("${logbackVersion}")
                }
                if (requested.group.startsWith("jakarta.xml.bind"))  {
                    details.useVersion("${jaxbApiVersion}")
                }
                if (requested.name == "jakarta.servlet.jsp.jstl-api") {
                    details.useVersion("${jakartaJstlVersion}")
                } else if (requested.group.startsWith("jakarta.servlet"))  {
                    details.useVersion("${jakartaServletVersion}")
                }
                if (requested.group == "org.apache.httpcomponents")  {
                    if (details.requested.name == "httpcore") {
                        details.useVersion("${httpCoreVersion}")
                    } else {
                        details.useVersion("${httpclientVersion}")
                    }
                }
                if (requested.group.startsWith("com.fasterxml.jackson"))  {
                    details.useVersion("${jacksonVersion}")
                }
                if (requested.group == "io.micrometer" && requested.name.startsWith("micrometer-"))  {
                    details.useVersion("${micrometerVersion}")
                }
                if (requested.group == "org.apache.wss4j")  {
                    details.useVersion("${wss4jVersion}")
                }
                if (requested.group == "org.slf4j")  {
                    details.useVersion("${slf4jVersion}")
                }
                if (requested.group == "org.apache.logging.log4j") {
                    details.useVersion("${log4jVersion}")
                }
                if (requested.group == "org.apache.wss4j") {
                    details.useTarget group: "com.github.mmoayyed.ws-wss4j", name: requested.name, version: "${wss4jVersion}"
                    details.because "WSS4J library does not support OpenSAML v5"
                }
            }
        }
    }
    exclude(group: "cglib", module: "cglib")
    exclude(group: "cglib", module: "cglib-full")
    exclude(group: "org.slf4j", module: "slf4j-log4j12")
    exclude(group: "org.slf4j", module: "slf4j-simple")
    exclude(group: "org.slf4j", module: "jcl-over-slf4j")
    exclude(group: "org.apache.logging.log4j", module: "log4j-to-slf4j")
    exclude(group: "pull-parser", module: "pull-parser")
    exclude(group: "javax.servlet", module: "servlet-api")
    exclude(group: "javax.ws.rs", module: "javax.ws.rs-api")
    exclude(group: "javax.annotation", module: "javax.annotation-api")
    exclude(group: "javax.activation", module: "activation")
    exclude(group: "commons-logging", module: "commons-logging")
}
