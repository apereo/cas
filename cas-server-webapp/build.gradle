apply plugin: 'war'
apply plugin: "org.akhikhl.gretty"

description = 'Apereo CAS Web Application'

ext {
    jettySslConfigEnabled = project.hasProperty('jettySslKeyStorePath') && 
                            project.hasProperty('jettySslTrustStorePath') &&
                            project.hasProperty('jettySslTrustStorePassword') &&
                            project.hasProperty('jettySslKeyStorePassword')
}

javadoc {
    enabled false
}

war {
    manifest {
        attributes("Implementation-Title": project.name,
                "Implementation-Vendor": project.group,
                "Created-By": project.group,
                "Implementation-Date": new Date(),
                "Implementation-Version": project.version)
    }
}

dependencies {
    compile project(':cas-server-webapp-support')
    compile project(':cas-server-core')
    compile project(':cas-server-core-monitor')
    compile project(':cas-server-core-authentication')
    compile project(':cas-server-core-services')
    compile project(':cas-server-core-logout')
    compile project(':cas-server-core-audit')
    compile project(':cas-server-core-logging')
    compile project(':cas-server-core-tickets')
    compile project(':cas-server-core-web')
    compile project(':cas-server-core-validation')
    compile project(':cas-server-core-util')
    compile project(':cas-server-webapp-throttle')
    compile project(':cas-server-webapp-actions')
    compile project(':cas-server-webapp-cookie')
    compile project(':cas-server-webapp-reports')
    compile project(':cas-server-webapp-themes')
    compile project(':cas-server-webapp-validation')

    compile project(':cas-server-support-duo')
    compile project(':cas-server-support-yubikey')

    compile libraries.pac4j
    runtime libraries.spring
    runtime libraries.cassecurityfilter
    runtime libraries.groovy
    runtime libraries.metrics
    runtime libraries.hibernate
    runtime libraries.bouncycastle
}

gretty {
    jvmArgs = ["-Dorg.eclipse.jetty.annotations.maxWait=120","-Xdebug","-Xrunjdwp:transport=dt_socket,address=5000,server=y,suspend=n"]
    scanInterval = 5
    contextPath = '/cas'
    httpsEnabled = project.ext.jettySslConfigEnabled
    if (project.ext.jettySslConfigEnabled) {
        httpsPort = 8443
        sslKeyStorePath = project.ext.jettySslKeyStorePath
        sslKeyStorePassword = project.ext.jettySslKeyStorePassword
        sslTrustStorePath = project.ext.jettySslTrustStorePath
        sslTrustStorePassword = project.ext.jettySslTrustStorePassword
    }
}
