description = "Apereo CAS Core Configuration Model"
def skipConfigurationMetadata = providers.systemProperty("skipNestedConfigMetadataGen").forUseAtConfigurationTime().present

sourceSets.test.resources.srcDirs += sourceSets.main.resources.srcDirs

dependencies {
    implementation libraries.jasypt
    implementation libraries.bouncycastle

    implementation libraries.springbootconfigmetadata

    api project(":api:cas-server-core-api-util")
    api project(":api:cas-server-core-api-protocol")
    
    testImplementation libraries.springbootconfigmetadata
    
    compileOnlyApi libraries.javaparser
    
    if (!skipConfigurationMetadata) {
        annotationProcessor libraries.springbootconfigmetadata
    }
}

if (!skipConfigurationMetadata) {
    task generateConfigurationMetadata(type: JavaExec) {
        doFirst {
            logger.info "\nRunning CAS Spring configuration metadata processor in addition to the native " +
                    "Spring Boot metadata processor, to create configuration " +
                    "metadata for nested and indexed properties. The process starts with the build directory " +
                    "at [$buildDir] for the CAS configuration module at [$projectDir.canonicalPath].\n"
        }
        mainClass.set("org.apereo.cas.configuration.metadata.ConfigurationMetadataGenerator")
        classpath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath
        args buildDir, projectDir.canonicalPath
    }

    compileJava.inputs.files(processResources).withPropertyName("processResources")
        .withPathSensitivity(PathSensitivity.RELATIVE).ignoreEmptyDirectories()
    jar.dependsOn(generateConfigurationMetadata)
}
