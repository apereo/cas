description = "Apereo CAS Core Configuration Model"
def skipConfigurationMetadata = providers.systemProperty("skipNestedConfigMetadataGen").present

sourceSets.test.resources.srcDirs += sourceSets.main.resources.srcDirs

sourceSets {
    configurationMetadata {
        resources {
            srcDir 'src/config'
        }
    }
}

dependencies {
    implementation libraries.jasypt
    implementation libraries.bouncycastle

    implementation libraries.springbootconfigmetadata

    api project(":api:cas-server-core-api-util")
    api project(":api:cas-server-core-api-protocol")
    
    testImplementation libraries.springbootconfigmetadata
    
    compileOnly libraries.javaparser
    compileOnly libraries.log4j
    
    if (!skipConfigurationMetadata) {
        annotationProcessor libraries.springbootconfigmetadata
    }
}

if (!skipConfigurationMetadata) {
    tasks.register('generateConfigurationMetadata', JavaExec) {
        def projectPath = projectDir.canonicalPath
        doFirst {
            logger.info "\nRunning CAS Spring configuration metadata processor in addition to the native " +
                    "Spring Boot metadata processor, to create configuration " +
                    "metadata for nested and indexed properties. The process starts with the build directory " +
                    "at [$project.layout.buildDirectory] for the CAS configuration module at [$projectPath].\n"
        }
        mainClass.set("org.apereo.cas.configuration.metadata.ConfigurationMetadataGenerator")

        def sortedClasspath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath + sourceSets.configurationMetadata.runtimeClasspath
        classpath = sortedClasspath.sort()

        args project.layout.buildDirectory.getAsFile().get().path, projectDir.canonicalPath
    }

    compileJava.inputs.files(processResources).withPropertyName("processResources")
        .withPathSensitivity(PathSensitivity.RELATIVE).ignoreEmptyDirectories()
    jar.dependsOn(generateConfigurationMetadata)
}
