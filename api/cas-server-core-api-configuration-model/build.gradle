description = "Apereo CAS Core Configuration Model"
def skipConfigurationMetadata = providers.systemProperty("skipNestedConfigMetadataGen").present

sourceSets.test.resources.srcDirs += sourceSets.main.resources.srcDirs

sourceSets {
    configurationMetadata {
        resources {
            srcDir 'src/config'
        }
    }
}

dependencies {
    implementation libraries.jasypt
    implementation libraries.bouncycastle

    implementation libraries.springbootconfigmetadata

    api project(":api:cas-server-core-api-util")
    api project(":api:cas-server-core-api-protocol")
    
    testImplementation libraries.springbootconfigmetadata

    compileOnly libraries.javaparser
    compileOnly libraries.log4j
    
    if (!skipConfigurationMetadata) {
        annotationProcessor libraries.springbootconfigmetadata
    }
}

if (!skipConfigurationMetadata) {
    def outputFile = project.layout.buildDirectory.file("generated/spring-configuration-metadata/META-INF/spring-configuration-metadata.json")

    tasks.register('generateConfigurationMetadata', JavaExec) {
        inputs.files(tasks.named('processResources'), tasks.named('compileJava'), tasks.named('jar'))

        outputs.cacheIf { true }

        outputs.file(outputFile)

        mainClass.set("org.apereo.cas.configuration.metadata.ConfigurationMetadataGenerator")

        def sortedClasspath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath + sourceSets.configurationMetadata.runtimeClasspath
        classpath = files(sortedClasspath.sort())

        args project.layout.buildDirectory.getAsFile().get().path, projectDir.canonicalPath
    }
    sourceSets.main.output.dir project.layout.buildDirectory.file("generated/spring-configuration-metadata"), builtBy: "generateConfigurationMetadata"
    
    tasks.named('compileJava') {
        inputs.files(tasks.named('processResources'))
    }
    jar.dependsOn(generateConfigurationMetadata)

    jar {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}
